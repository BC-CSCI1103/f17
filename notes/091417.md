# CSCI 1103 Computer Science I Honors

### Fall 2017

---

## Lecture Notes
## Week 3, Meeting 2

**Topics:**

    1. Repetition & Work
    2. Lists

---

## 1. Repetition & Work

Last time

```ocaml
let rec fibo n =
  match n < 3 with
  | true  -> 1
  | false -> (fibo (n - 1)) + (fibo (n - 2))
  
let rec fastFibo n a b =
  match n = 1 with
  | true  -> a
  | false -> fastFibo (n - 1) b (a + b)
```

Simplifying an example call `(fibo 3)` requires 12 steps:

```ocaml
(fibo 3) -> match 3 < 3 with | true -> 1 | false -> (fibo (3 - 1)) + (fibo (3 - 2))
         -> match false with | true -> 1 | false -> (fibo (3 - 1)) + (fibo (3 - 2))
         -> (fibo (3 - 1)) + (fibo (3 - 2))
         -> (fibo 2) + (fibo (3 - 2))
         -> (match 2 < 3 with | true -> 1 | false -> (fibo (2 - 1)) + (fibo (2 - 2))) + (fibo (3 - 2))
         -> (match true with | true -> 1 | false -> (fibo (2 - 1)) + (fibo (2 - 2))) + (fibo (3 - 2))
         -> 1 + (fibo (3 - 2))
         -> 1 + (fibo 1)
         -> 1 + (match 1 < 3 with | true -> 1 | false -> (fibo (1 - 1)) + (fibo (1 - 2)))
         -> 1 + (match true with | true -> 1 | false -> (fibo (1 - 1)) + (fibo (1 - 2)))
         -> 1 + 1
         -> 2
```

How many simplification steps are required in general? Well, we can focus just on the number of addition steps and we can view the number of computation steps as a function of the input size. We can gain some perspective on this by viewing a call of `fibo` as a "tree" (albeit, up-side-down). For example, the call `(fibo 6)`:

```ocaml
1, 1, 2, 3, 5, 8:
                    fibo 6 = +                       NB: The (fibo 3) tree has 1 add step;
                         /       \                       The (fibo 4) tree has 2 add steps;
               fibo 5 = +         + = fibo 4             The (fibo 5) tree has 4 add steps;
                      /   \      /  \                    The (fibo 6) tree has 7 add steps.
           fibo 4 =  +     +    +    1 = fibo 2
                   /  \   / \  / \
         fibo 3 = +    1 1   1 1  1 = fibo 1
                 / \
       fibo 2 = 1   1 = fibo 1
```

The tree for `fibo 7` would have a copy of the tree for `fibo 6` on the left and a copy of the tree for `fibo 5` on the right. Their results would be combined with an add step. The tree for `fibo 7` has 12 add steps. 

Let's write the number of steps required for `(fibo N)` as `steps(fibo, N)`. For N = 1, 2, `steps(fibo, N) = 0`. For N > 2:

```
steps(fibo, N) = steps(fibo, N - 1) + steps(fibo, N - 2)) + 1
```

#### fastFibo

```ocaml
fastFibo 3 1 1 -> match 3 = 1 with | true -> 1 | false -> fastFibo (3 - 1) 1 (1 + 1)
               -> match false with | true -> 1 | false -> fastFibo (3 - 1) 1 (1 + 1)
               -> fastFibo (3 - 1) 1 (1 + 1)
               -> fastFibo 2 1 (1 + 1)               (* ADD STEP *)
               -> fastFibo 2 1 2
               -> match 2 = 1 with | true -> 1 | false -> fastFibo (2 - 1) 2 (1 + 2)
               -> match false with | true -> 1 | false -> fastFibo (2 - 1) 2 (1 + 2)
               -> fastFibo (2 - 1) 2 (1 + 2)
               -> fastFibo 1 2 (1 + 2)               (* ADD STEP *)
               -> fastFibo 1 2 3
               -> match 1 = 1 with | true -> 2 | false -> fastFibo (1 - 1) 3 (2 + 3)
               -> match true with | true -> 2 | false -> fastFibo (1 - 1) 3 (2 + 3)
               -> 2
```

The number of simplification, roughly speaking, the *amount of work*, is characterized as a function of the input size.

|  N   |     F(N)      | `(fibo N)` steps | `(fastFibo N 1 1)` steps |
| :--: | :-----------: | :--------------: | :----------------------: |
|  1   |       1       |        0         |            0             |
|  2   |       1       |        0         |            0             |
|  3   |       2       |        1         |            2             |
|  4   |       3       |        2         |            3             |
|  5   |       5       |        4         |            4             |
|  6   |       8       |        7         |            5             |
|  7   |      13       |        12        |            6             |
|  8   |      21       |        20        |            7             |
| ...  |      ...      |       ...        |           ...            |
|  45  | 1,134,903,170 |  ~1,134,903,170  |            44            |

`fibo` and `fastFibo` compute the same mathematical function:

```
F(N) = {(1, 1), (2, 1), (3, 2), (4, 3), (5, 5), (6, 8), (7, 13), (8, 21), ... }
```

But the work performed by the former makes it infeasible for even moderately sized N while the work performed by the latter is quite acceptable. I.e., it is fast and scales well to large inputs.

#### Making fastFibo easier to use

We'd prefer that a user of `fastFibo` not have to worry about providing the initial `a` and `b` inputs so we would normally write `fastFibo` as a function of only one argument `n` as follows:

```ocaml
let fastFibo n =
  let rec repeat n a b =
    match n = 1 with
    | true  -> a
    | false -> repeat (n - 1) b (a + b)
  in
  repeat n 1 1
```

We've introduced a special 3-argument helper function called `repeat` which is usable only within `fastFibo`. This will be a common pattern, it provides a better interface to `fastFibo` and it hides the name `repeat` within the definition of `fastFibo`. Managing the visibility of names turns out to be quite important so it's good to be able to localize them in this way.



---

## 2. Lists

We've seen base types `int`, `float`, `unit`, `bool`, `char` and `string`. For non-base types we've seen the "either/or" (i.e., *union*, *sum* or *variant*) types, we've seen *product* types (i.e., tuples) and we've seen fancier product types where the parts have names, i.e., *record* types. 

We're now going to see one of the most important built-in types: the *list*. In OCaml, a list is a special-case of an either/or type. It comes with special built-in syntax.

```ocaml
# let odds = [1; 3; 5];;
val odds : int list = [1; 3; 5]

# List.length odds;;
- : int = 3

# List.mem 3 odds;;
- : bool = true

# List.rev odds;;
- : int list = [5; 3; 1]

# odds;;
- : int list = [1; 3; 5]                 (* odds wasn't changed/mutated by List.rev *)

# let fiveOdds = odds @ [7; 9];;         (* append *)
val fiveOdds = [1; 3; 5; 7; 9]

# let lincoln = ["Four"; "score"; "and"; "seven"; "years"; "ago"]
val lincoln : string list = ["Four"; "score"; "and"; "seven"; "years"; "ago"]

# type fruit = Durian | Lemon | Mangosteen | Orange | Lychee

# let fruits = [Lemon; Durian; Lychee; Lemon];;
val fruits : fruit list = [Lemon; Durian; Lychee; Lemon];;
```

Lists can contain values of any type but values of mixed types within a list aren't allowed.

```ocaml
# [2; 2.5];;
Error: This expression has type float but an expression was expected of type
         int
```

As we'll explain below, this restriction can usually be circumvented.

```ocaml
# [];;                         (* The empty list, called "nil" *)
- : 'a list = []

# 2 :: [];;                    (* The "cons" operator ::, when given a list element and a list, *)
- : int list = [2]             (* it constructs a new list. *)

# 2 :: 4 :: [];;
- : int list = [2; 4]

# [1 + 1; 2 + 2];;
- : int list = [2; 4]

# (1 + 1) :: (2 + 2) :: [];;
- : int list = [2; 4]
```

Like the type `bool` the list type is a special built-in either-or (union/sum/variant) type. A list is either [] or a cons — there are no other forms of lists. Like many modern programming languages, OCaml accepts simple bracket notation `[1; 2; 3]` for lists in order to make the code easier to type and read. Without it, we'd have to type `1 :: 2 :: 3 :: []`. 

> In Python and Javascript one would type a list as `[1, 2, 3]` with the elements separated by commas. But it turns out that lists in Python and Javascript actually correspond to *arrays* in OCaml (and Java). We'll come back to this topic later.

#### Lists with values of mixed types

How to populate a list with values of mixed type? When the types in question are known ahead of time, one can simply define a union type. 

```ocaml
# type number = Flt of float | Int of int

# Flt 2.5;;
- : number = Flt 2.5

# Int 2;;
- : number = Int 2

# let numbers = [Int 2; Flt 2.5];;
val numbers : number list = [Int 2; Flt 2.5]
```

The list is now populated with values of type `number`.

### Functions on Lists

Let `xs` be a list and let `f` be a function accepting `xs`:

```ocaml
let f xs = ...
```

A list can have zero elements or some number of elements that might vary from one call of `f` to another. This means that most functions working on lists involve *repetition* — some number of computation steps need to be taken for each element of `xs`. It is a touchstone of OCaml and functional programming languages in general that this sort of repetition is implemented by writing `f` as a *recursive function*, i.e., one that can refer to itself in its own definition.

Recursive functions are incredibly powerful and fun to write. When they're writen well, they can be quite elegant while still being very efficient.  In OCaml, the keyword `rec` is required to distinguish a recursive function definition from the definition of a non-recursive function:

```ocaml
let rec f xs = ...
```

We'll start with a simple function that adds a list of integers:

```ocaml
(* add : int list -> int
 *)
let rec add ns =
  match ns with
  | [] -> 0
  | m :: ms -> m + add ms
```

The idea here is to identify the simplest case first, in this case when `ns` is `[]`. Such a case is usually called a *base case*. The non-base case, the *recursive case*, is when `ns` is a cons `m :: ms`. This is called the recursive case because because the value of `ms` is just like the value of `ns` — a list of integers, but with one fewer elements. **To add the elements in `ms` we're invited to use the `add` function that we're presently writing.** Tracing a call `add [1; 2]`:

```ocaml
add [1; 2] -> match [1; 2] with | [] -> 0 | m :: ms -> m + add ms
           =  match 1 :: (2 :: []) with | [] -> 0 | m :: ms -> m + add ms     (* For explaining *)
           -> 1 + add (2 :: [])
           -> 1 + (match (2 :: []) with | [] -> 0 | m :: ms -> m + ms)
           -> 1 + (2 + (add []))
           -> 1 + (2 + (match [] with | [] -> 0 | m :: ms -> m + ms))
           -> 1 + (2 + 0)
           -> 1 + 2
           -> 3
```

When writing repetitive functions, you should always be mindful of the amount of work each function requires. 

>How much work does the `add` function do?

Revisiting the `number` example above, let's write a function that yields the floating point sum of a list of numbers:

```ocaml
(* addNumbers : number list -> float
 *)
let rec addNumbers ns =
  match ns with
  | [] -> 0.0
  | (Int m) :: ms -> (float m) +. addNumbers ms
  | (Flt m) :: ms ->        m  +. addNumbers ms
```

Let's look at how some of the `List` module functions might be implemented.

```ocaml
let rec length xs =
  match xs with
  | [] -> 0
  | y :: ys -> 1 + length ys
  
let rec mem x xs =
  match xs with
  | [] -> false
  | y :: ys -> (match x = y with
                | true  -> true
                | false -> mem x ys)
                
let rec append xs ys =
  match xs with
  | [] -> ys
  | z :: zs -> z :: append zs ys
```

> 1. How much work does the `length` function do?
> 2. How much work does the `mem` function do?
> 3. How much work does the `append` function do?