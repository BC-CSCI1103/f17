# CSCI 1103 Computer Science I Honors

### Fall 2017

---

## Lecture Notes
## Week 4, Meeting 2

**Topics:**

1. Review & Consolidation — Four Exercises

---

1. **power**: Four versions of the integer exponentiation function `power : int -> int -> int` which computes $m^n$ for integers $m$ and $n$. The function names have numerical suffixes for explanatory purposes.

```ocaml
(* power : int -> int -> int
*)
let rec power1 m n =
  match n = 0 with
  | true  -> 1
  | false -> m * power1 m (n - 1)
  
let power2 m n =                             (* NB: symbols m and n are variables wrt power2 *)
  let rec power3 n answer =
    match n = 0 with
    | true  -> answer
    | false -> power3 (n - 1) (m * answer)   (* NB: this occurrence of m is constant wrt power3 *)
  in
  power3 n 1
  
let rec power4 m n =                         (* using an if-expression instead of match *)
  if n = 0 then
    1
  else
    m * power4 m (n - 1)
    
let rec power5 m = function                  (* using the function form for matching *)
  | 0 -> 1
  | n -> m * power5 m (n - 1)
```

**Tracing** — `power1`, `power4` and `power5` all have the same widening computational shape. `power2` is straight-line (i.e., tail-recursive). In practical terms, `power2` uses computer memory more efficiently. In particular, `power2` uses a constant amount of memory while `power1`, `power4` and `power5` all require an amount of memory that grows linearly with `n`.

```ocaml
power1 2 3 -> 
  2 * (power1 2 2) ->
  2 * (2 * (power1 2 1)) ->
  2 * (2 * (2 * (power1 2 0))) ->
  2 * (2 * (2 * 1)) ->
  2 * (2 * 2) ->
  2 * 4 ->
  8
  
power2 2 3 ->
  power3 3 1 ->
  power3 2 2 ->
  power3 1 4 ->
  power3 0 8 ->
  8
```

2. **nth**: Two versions of the function `nth : 'a list -> int -> 'a`. The first uses the built-in library functions `List.hd : 'a list -> 'a` and `List.tl : 'a list -> 'a list`. The second version matches on two items by bundling them together in a pair.

```ocaml
(* nth : 'a list -> int -> 'a
*)
let rec nth xs n =
  match n = 0 with
  | true  -> List.hd xs
  | false -> nth (List.tl xs) (n - 1)
  
let rec nth xs n =
  match (n = 0, xs) with                        (* NB: matching a pair here *)
  | (true,  x :: xs) -> x
  | (false, _ :: xs) -> nth xs (n - 1)
  | _ -> failwith "nth: bad input"
```

3. **zip** & **unzip**: these functions are closely related to OCaml's `List.combine` and `List.split`. NB that the `zip` and `unzip` functions defined here are not inverses of one another because `zip` doesn't accept a pair of lists as inputs.

```ocaml
(* zip : 'a list -> 'b list -> ('a * 'b) list
*)
let rec zip xs ys =
  match (xs, ys) with
  | ([], []) -> []
  | (x :: xs, y :: ys) -> (x, y) :: zip xs ys
  
(* unzip : ('a * 'b) list -> ('a list * 'b list)
*)
let rec unzip xys =
  match xys with
  | [] -> ([], [])
  | (x, y) :: xys ->
    let (xs, ys) = unzip xys
    in
    (x :: xs, y :: ys)
```

4. **sublists**: Given a list `xs` find all of the sublists `sublists : 'a list -> ('a list) list`.

```ocaml
let rec spread x xxs =
  match xxs with
  | [] -> []
  | ys :: yys -> (x :: ys) :: spread x yys
  
(* sublists : 'a list -> ('a list) list
*)
let rec sublists xs =
  match xs with
  | [] -> [[]]
  | x :: xs -> let ys = sublists xs
               in
               (spread x ys) @ ys
```

