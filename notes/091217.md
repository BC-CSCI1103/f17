# CSCI 1103 Computer Science I Honors

### Fall 2017

---

## Lecture Notes
## Week 3, Meeting 1

**Topics:**

    1. Simplification of Function Calls and let-in Expressions
    2. Pattern Matching
    3. Type Definitions, Unions/Variants/Sums & match expressions
    4. Records

---

### 1. Simplification of Function Calls and let-in Expressions

Last time

```ocaml
let div m n =
  let quotient = m / n in
  let remainder = m mod n
  in
  (quotient, remainder)
```

Simplifying function calls and let-in:

```ocaml
div (3 + 5) (2 * 3) --> div 8 (2 * 3)
                    --> div 8 6
                    --> let quotient = 8 / 6 in let remainder = 8 mod 6 in (quotient, remainder)
                    --> let quotient = 1     in let remainder = 8 mod 6 in (quotient, remainder)
                    --> let remainder = 8 mod 6 in (1, remainder)
                    --> let remainder = 2 in (1, remainder)
                    --> (1, 2)                                 (* a value, obtained in 7 steps *)
```



---

### 2. Pattern Matching

```ocaml
let (m, n) = (1 + 1, 2 + 2) in m + n --> let (m, n) = (2, 2 + 2) in m + n
                                     --> let (m, n) = (2, 4) in m + n
                                     --> 2 + 4
                                     --> 6
```



---

### 3. Type Declarations, Union Types & Match Expressions

A value of type `bool` is **either** `true` **or** `false`.

```ocaml
type bool = true | false
```

The vertical bar is pronounced "or". Mathematically the type `bool` is an example of a type denoting a *set* that is a *disjoint union* of sets. In programming it is sometimes called a *union* type, a *sum* type or a *variant* type.

#### Match Expressions

```ocaml
# match true with | true -> 0 | false -> 1;;
- : int = 0

# match 2 = 3 with | true -> "Yes" | false -> "No";;
- : string = "No"
```

```ocaml
# let coin = match Random.int 2 = 0 with | true -> "heads" | false -> "tails";;
val coin : string = "heads"
```

#### Simplification of Match Expressions

```ocaml
match Random.int 2 = 0 with | true -> "heads" | false -> "tails" -->
match 1 = 0 with | true -> "heads" | false -> "tails" -->
match false with | true -> "heads" | false -> "tails" -->
"tails"
```

#### Type Declarations 


We can use the keyword `type` to declare new types.

```ocaml
# type coin = Heads | Tails    (* NB: Heads & Tails start with capital letters.   *)
type coin = Heads | Tails      (* The only values of type coin are Heads & Tails. *)

# Heads;;
- : coin = Heads

# (Heads, Tails);;
- : coin * coin = (Heads, Tails)
```

We can think of `Heads` and `Tails` as *constants* of type `coin`.  In general, these symbols are called *constructors* because they construct a value of type `coin`.


```ocaml
(* flip : unit -> coin
 *)
let flip () =
  match Random.int 2 = 0 with              (* A random integer in the range 0..1 *)
  | true  -> Heads
  | false -> Tails
```

We 

```ocaml
type fruit = Durian | Lemon | Mangosteen | Orange | Lychee

# Lemon;;
- : fruit = Lemon

(* isCitrus : fruit -> bool
 *)
let isCitrus fruit =
  match fruit with
  | Durian -> false
  | Lemon -> true
  | Mangosteen -> false
  | Orange -> true
  | Lychee -> false

let isCitrus fruit =
  match fruit with
  | Lemon | Orange -> true                 (* NB: Two cases with the same outcome.       *)
  | _ -> false                             (* NB: _ is a wildcard, it matches anything.  *)
```

Some constructors would naturally require some sort of input to build their values. For example, a circle requires a radius.

````ocaml
type shape = Circle of float

# Circle 1.0;;
- : shape = Circle 1.0

(* area : shape -> float
 *)
let area (Circle radius) = 3.14 *. radius ** 2.0

type shape = Circle of float | Square of float

(* area : shape -> float
 *)
let area shape = 
  match shape with
  | Circle radius -> 3.14 *. radius ** 2.0
  | Square side -> side *. side

````

> If you use a match expression to process a variant and you don't cover all of the cases, OCaml will issue a warning. This is extremely helpful.

It's natural to allow constructors to accept many inputs. For example, a ring would require a radius and a width. We can do this using tuples:

```ocaml
type shape = Circle of float
           | Square of float
           | Ring of float * float          (* Ring requires a 2-tuple, a pair, of floats. *)
           
# Ring (100.0, 12.0);;
- : shape = Ring (100., 12.)
```

#### 4. Records

A record is like a tuple but the parts have symbolic names.

```ocaml
type point = {x : float; y : float}          (* A point has an x field and a y field *)

# let p = {x = 1.0; y = 2.0};
val p : point = {x = 1.; y = 2.}

# p.x;;
- : float = 1.0

# p.y;;
- : float = 2.0

let {x; y} = p in x +. y;;
- : float = 3.0
```



```ocaml
type shape = Circle of float
           | Ring of {radius : float; width : float}
           | Rectangle of {width : float; height : float}
           | EquilateralTriangle of side
```

